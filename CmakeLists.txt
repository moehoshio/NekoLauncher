cmake_minimum_required(VERSION 3.20)

# Set policy for MSVC runtime library (must be before project())
if(POLICY CMP0091)
    cmake_policy(SET CMP0091 NEW)
endif()

project(
    NekoLc LANGUAGES CXX VERSION 1.0.0.0
    DESCRIPTION "Neko Launcher Client"
    HOMEPAGE_URL "https://github.com/moehoshio/NekoLauncher"
)

# ================
# === Config ====
# ================

option(NEKO_LC_STATIC_LINK "Neko Launcher Static Link Libraries" ON)
option(NEKO_LC_BUILD_TESTS "Neko Launcher Build tests" ON)

# Package path. e.g. vcpkg
if(NOT DEFINED NEKO_LC_LIBRARY_DIRS)
    set(NEKO_LC_LIBRARY_DIRS "/path/to/vcpkg/installed/x64-windows")
endif()

# Qt path
if(NOT DEFINED Qt6_DIR)
    set(Qt6_DIR "/path/to/Qt6/6.6.1/msvc2019_64")
endif()


list(APPEND CMAKE_PREFIX_PATH 
    ${NEKO_LC_LIBRARY_DIRS}
    ${Qt6_DIR}
)

set(CMAKE_AUTOMOC ON)
set(CMAKE_AUTORCC ON)
set(CMAKE_AUTOUIC ON)

# ================
# === Library ====
# ================


if(NEKO_LC_STATIC_LINK AND MSVC)
    # Set MSVC runtime library to static for all targets (must be before any target creation)
    set(CMAKE_MSVC_RUNTIME_LIBRARY "MultiThreaded$<$<CONFIG:Debug>:Debug>")
endif()

find_package(Boost COMPONENTS process system)
find_package(Qt6 COMPONENTS Core Gui Widgets)
find_package(GTest QUIET)

if (NOT Boost_FOUND)
    message(FATAL_ERROR "Required Boost but not found ; please make sure to set -DNEKO_LC_LIBRARY_DIRS=<path> correctly.")
endif()

if (NOT Qt6_FOUND)
    message(FATAL_ERROR "Required Qt6 but not found ; please make sure to set -DQt6_DIR=<Qt_install_path> correctly.")
endif()

# Fetch Dependencies
include(FetchContent)

if(NOT GTEST_FOUND AND NEKO_LC_BUILD_TESTS)
    message(STATUS "GTest Not Found; Neko Launcher is fetching GoogleTest...")

    FetchContent_Declare(
        googletest
        GIT_REPOSITORY https://github.com/google/googletest.git
        GIT_TAG        v1.17.0
    )
    # For Windows: Prevent overriding the parent project's compiler/linker settings
    set(gtest_force_shared_crt ON CACHE BOOL "" FORCE)
    FetchContent_MakeAvailable(googletest)
endif()

# Other Libraries
FetchContent_Declare(
    nlohmann_json
    GIT_REPOSITORY https://github.com/nlohmann/json.git
    GIT_TAG        v3.11.3
)
FetchContent_Declare(
    simpleini
    GIT_REPOSITORY https://github.com/brofield/simpleini.git
    GIT_TAG        v4.22
)
FetchContent_MakeAvailable(simpleini nlohmann_json)

# Neko Modules
FetchContent_Declare(
    NekoSchema
    GIT_REPOSITORY https://github.com/moehoshio/NekoSchema.git
    GIT_TAG        main
)
FetchContent_Declare(
    NekoEvent
    GIT_REPOSITORY https://github.com/moehoshio/NekoEvent.git
    GIT_TAG        main
)
FetchContent_Declare(
    NekoThreadPool
    GIT_REPOSITORY https://github.com/moehoshio/NekoThreadPool.git
    GIT_TAG        main
)
FetchContent_Declare(
    NekoFunction
    GIT_REPOSITORY https://github.com/moehoshio/NekoFunction.git
    GIT_TAG        main
)
FetchContent_Declare(
    NekoSystem
    GIT_REPOSITORY https://github.com/moehoshio/NekoSystem.git
    GIT_TAG        main
)
FetchContent_Declare(
    NekoNet
    GIT_REPOSITORY https://github.com/moehoshio/NekoNet.git
    GIT_TAG        main
)
if (NOT DEFINED NEKO_LIBRARY_DIRS)
    set(NEKO_FUNCTION_LIBRARY_PATH ${NEKO_LC_LIBRARY_DIRS} CACHE PATH "Path to NekoFunction library")
    set(NEKO_NET_LIBRARY_PATH ${NEKO_LC_LIBRARY_DIRS} CACHE PATH "Path to NekoNet library")
endif()
FetchContent_MakeAvailable(NekoSchema NekoEvent NekoThreadPool NekoFunction NekoSystem NekoNet)

# ================
# ====  Info  ====
# ================

# default build type to Release
if(NOT CMAKE_BUILD_TYPE)
    set(CMAKE_BUILD_TYPE Release CACHE STRING "Choose the type of build.")
endif()

# Get the current git commit ID and build time
string(TIMESTAMP NEKO_LC_BUILD_TIME "%Y%m%d%H%M%S" UTC)
execute_process(COMMAND git rev-parse --short HEAD OUTPUT_VARIABLE NEKO_LC_GIT_COMMIT_ID OUTPUT_STRIP_TRAILING_WHITESPACE)
add_definitions(
    -DNEKO_LC_NAME="${PROJECT_NAME}"
    -DNEKO_LC_VERSION="${PROJECT_VERSION}"
    -DNEKO_LC_BUILD_TIME="${NEKO_LC_BUILD_TIME}"
    -DNEKO_LC_BUILD_TYPE="${CMAKE_BUILD_TYPE}"
    -DNEKO_LC_GIT_COMMIT_ID="${NEKO_LC_GIT_COMMIT_ID}"
)

# Print configuration summary
message(STATUS "NekoLauncher configuration summary:")
message(STATUS "  - CMake version: ${CMAKE_VERSION}")
message(STATUS "  - Build type: ${CMAKE_BUILD_TYPE}")
message(STATUS "  - Git commit ID: ${NEKO_LC_GIT_COMMIT_ID}")
message(STATUS "  - Project version: ${PROJECT_VERSION}")
message(STATUS "  - C++ Compiler: ${CMAKE_CXX_COMPILER_ID} ${CMAKE_CXX_COMPILER_VERSION}")
message(STATUS "  - Build time (UTC): ${NEKO_LC_BUILD_TIME}")
message(STATUS "")
message(STATUS "Options:")
message(STATUS "  - Neko Launcher Build tests: ${NEKO_LC_BUILD_TESTS}")
message(STATUS "")
message(STATUS "Dependency summary:")
message(STATUS "  - Boost found: ${Boost_FOUND} version ${Boost_VERSION}")
message(STATUS "  - Qt6 found: ${Qt6_FOUND} version ${Qt6_VERSION}")
message(STATUS "  - CMAKE_PREFIX_PATH: ${CMAKE_PREFIX_PATH}")
message(STATUS "  - Neko Library dirs: ${NEKO_LIBRARY_DIRS}")
message(STATUS "  - Qt6 dir: ${Qt6_DIR}")
message(STATUS "  - STL dir: ${STL_DIR}")
message(STATUS "Neko Modules: NekoEvent, NekoNet, NekoSchema, NekoSystem, NekoFunction, NekoThreadPool")
message(STATUS "")


# ================
# === Add Src ====
# ================

# Include directories
list(APPEND NEKO_INCLUDE_DIRS
    ${CMAKE_CURRENT_SOURCE_DIR}/include
    ${NEKO_LC_LIBRARY_DIRS}/include
    ${STL_DIR}/include
    ${simpleini_SOURCE_DIR}
)

# Src files
list(APPEND NEKO_SRCFILES
    # Core

    ${CMAKE_CURRENT_SOURCE_DIR}/src/neko/main.cpp
)


# ================
# ====  Test  ====
# ================

if(NEKO_LC_BUILD_TESTS)
    enable_testing()
    message(STATUS "Neko Launcher tests enabled (NEKO_LC_BUILD_TESTS=ON)")
    include(GoogleTest)

    # =====================
    # == NekoLc App Test ==
    # =====================

    # Api
    add_executable(NekoLcApp_Api_test "${CMAKE_CURRENT_SOURCE_DIR}/tests/neko_lc_app_api_test.cpp")
    target_include_directories(NekoLcApp_Api_test PRIVATE ${NEKO_INCLUDE_DIRS})
    target_link_libraries(NekoLcApp_Api_test PRIVATE GTest::gtest GTest::gtest_main nlohmann_json NekoSchema)
    target_compile_features(NekoLcApp_Api_test PRIVATE cxx_std_20)
    gtest_discover_tests(NekoLcApp_Api_test)

    # NekoLc Bus Test
    # add_executable(NekoLcBus_test "${CMAKE_SOURCE_DIR}/tests/neko_lc_bus_test.cpp")
    # target_include_directories(NekoLcBus_test PRIVATE ${NEKO_INCLUDE_DIRS})
    # target_link_libraries(NekoLcBus_test PRIVATE gtest_main nlohmann_json NekoSchema NekoEvent NekoNet NekoSystem NekoFunction NekoThreadPool)
    # target_compile_features(NekoLcBus_test PRIVATE cxx_std_20)
    # gtest_discover_tests(NekoLcBus_test)


    # NekoLc Schema Test
    # add_executable(NekoLcSchema_test "${CMAKE_SOURCE_DIR}/tests/neko_lc_schema_test.cpp")
    # target_include_directories(NekoLcSchema_test PRIVATE ${NEKO_INCLUDE_DIRS})
    # target_link_libraries(NekoLcSchema_test PRIVATE gtest_main nlohmann_json NekoSchema NekoEvent NekoNet NekoSystem NekoFunction NekoThreadPool)
    # target_compile_features(NekoLcSchema_test PRIVATE cxx_std_20)
    # gtest_discover_tests(NekoLcSchema_test)

    # Core - Update Test
    # add_executable(NekoCore_update_test "${CMAKE_SOURCE_DIR}/tests/neko_core_update_test.cpp")
    # target_include_directories(NekoCore_update_test PRIVATE ${NEKO_INCLUDE_DIRS})
    # target_link_libraries(NekoCore_update_test PRIVATE gtest_main NekoSchema NekoEvent NekoNet NekoSystem NekoFunction NekoThreadPool)
    # target_compile_features(NekoCore_update_test PRIVATE cxx_std_20)
    # gtest_discover_tests(NekoCore_update_test)
else()
    message(STATUS "Neko Launcher tests disabled (NEKO_LC_BUILD_TESTS=OFF)")
endif()


# ================
# == Executable ==
# ================

# Add the executable target
add_executable(NekoLc ${NEKO_SRCFILES})

# Set the target properties
target_include_directories(NekoLc PRIVATE ${NEKO_INCLUDE_DIRS})
target_compile_features(NekoLc PRIVATE cxx_std_20)

# Link Neko Modules
target_link_libraries(NekoLc PRIVATE 
    NekoSchema NekoEvent NekoSystem NekoFunction NekoThreadPool NekoNet
)
# Link libraries
target_link_libraries(NekoLc PRIVATE 
    # Qt
    Qt6::Core Qt6::Widgets Qt6::Gui
    # Boost
    Boost::system Boost::process
    # Others
    nlohmann_json SimpleIni
)

# Os-specific settings
if(WIN32)
    target_sources(NekoLc PRIVATE resource/windows/icon.rc)
    
    # Set not to use the console window
    # if(CMAKE_BUILD_TYPE STREQUAL "Release")
    #     set_target_properties(NekoLc PROPERTIES WIN32_EXECUTABLE TRUE)
    # endif()
    target_sources(NekoLc PRIVATE resource/windows/icon.rc)

elseif(APPLE)

    # Set the application bundle properties for macOS
    if(CMAKE_BUILD_TYPE STREQUAL "Release")
        set_target_properties(NekoLc PROPERTIES
            MACOSX_BUNDLE TRUE
            MACOSX_BUNDLE_BUNDLE_NAME "com.moehoshio.NekoLauncher"
            # MACOSX_BUNDLE_ICON_FILE "NekoLc.icns"
            MACOSX_BUNDLE_INFO_PLIST ${CMAKE_SOURCE_DIR}/resource/macos/Info.plist
        )
    endif()
    set(MACOSX_ICON ${CMAKE_SOURCE_DIR}/resource/macos/NekoLc.icns)
    target_sources(NekoLc PRIVATE ${MACOSX_ICON})
    set_source_files_properties(${MACOSX_ICON} PROPERTIES MACOSX_PACKAGE_LOCATION "Resources")

elseif(UNIX)

    # Set the executable properties for Linux
    set_target_properties(NekoLc PROPERTIES
        OUTPUT_NAME "NekoLc"
        PREFIX ""
        SUFFIX ""
    )
    # Add a custom command to copy the icon file to the resource directory
    add_custom_command(TARGET NekoLc POST_BUILD
        COMMAND ${CMAKE_COMMAND} -E copy ${CMAKE_SOURCE_DIR}/resource/linux/icon.png ${CMAKE_BINARY_DIR}/icon.png
        COMMENT "Copying icon.png to build directory"
    )

endif() # os

        

# ================
# === Install ====
# ================

# Install rules
# install(TARGETS NekoLc
#     RUNTIME DESTINATION .
#     BUNDLE DESTINATION .
# )

# # Install configuration and resource files
# install(FILES ${CMAKE_SOURCE_DIR}/src/config.ini
#     DESTINATION .
# )

# # Install language files
# install(FILES 
#     ${CMAKE_SOURCE_DIR}/src/lang/en.json
#     ${CMAKE_SOURCE_DIR}/src/lang/zh_tw.json
#     ${CMAKE_SOURCE_DIR}/src/lang/zh_cn.json
#     DESTINATION lang
# )

# # Install image files
# install(FILES ${CMAKE_SOURCE_DIR}/resource/img/loading.gif
#     DESTINATION img
# )