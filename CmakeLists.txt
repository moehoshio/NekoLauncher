cmake_minimum_required(VERSION 3.20)
project(
    NekoLc LANGUAGES CXX VERSION 1.0.0.0
    DESCRIPTION "Neko Launcher Client"
    HOMEPAGE_URL "https://github.com/moehoshio/NekoLauncher"
)

# ================
# === Config ====
# ================

set(NEKO_PATH_SEPARATOR)

if (WIN32)
    set(NEKO_PATH_SEPARATOR ";")
else()
    set(NEKO_PATH_SEPARATOR ":")
endif()

option(NEKO_BUILD_TESTS "Neko Launcher Build tests" ON)

# Package path. e.g. vcpkg
set(NEKO_LIBRARY_DIRS "/path/to/vcpkg/installed/x64-windows")
# Qt path
set(Qt6_DIR "/path/to/Qt6/6.6.1/msvc2019_64")

set(STL_DIR "/path/to/STL")

# user program version
set(NEKO_LC_USER_VERSION "2.3.1.0")

set(CMAKE_PREFIX_PATH "${Qt6_DIR}${NEKO_PATH_SEPARATOR}${NEKO_LIBRARY_DIRS}")
set(CMAKE_AUTOMOC ON)

# ================
# === Library ====
# ================

find_package(Boost COMPONENTS system process)
find_package(Qt6 COMPONENTS Core Gui Widgets)
find_package(GTest QUIET)

if (NOT Boost_FOUND)
    message(FATAL_ERROR "Required Boost but not found ; if you installed it via vcpkg, please make sure to set -DNEKO_LIBRARY_DIRS=<vcpkg_install_path> correctly.")
endif()

if (NOT Qt6_FOUND)
    message(FATAL_ERROR "Required Qt6 but not found ; please make sure to set -DQt6_DIR=<Qt_install_path> correctly.")
endif()

# Fetch Dependencies
include(FetchContent)

if(NOT GTest_FOUND AND NEKO_BUILD_TESTS)
    message(STATUS "GTest not found. Fetching GoogleTest...")

    FetchContent_Declare(
        googletest
        GIT_REPOSITORY https://github.com/google/googletest.git
        GIT_TAG        release-1.12.1
    )
    # For Windows: Prevent overriding the parent project's compiler/linker settings
    set(gtest_force_shared_crt ON CACHE BOOL "" FORCE)
    FetchContent_MakeAvailable(googletest)
endif()

# Other Libraries
FetchContent_Declare(
    nlohmann_json
    GIT_REPOSITORY https://github.com/nlohmann/json.git
    GIT_TAG        v3.11.3
)
FetchContent_Declare(
    simpleini
    GIT_REPOSITORY https://github.com/brofield/simpleini.git
    GIT_TAG        v4.22
)
FetchContent_MakeAvailable(simpleini nlohmann_json)

# Neko Modules
FetchContent_Declare(
    NekoSchema
    GIT_REPOSITORY https://github.com/moehoshio/NekoSchema.git
    GIT_TAG        main
)
FetchContent_Declare(
    NekoEvent
    GIT_REPOSITORY https://github.com/moehoshio/NekoEvent.git
    GIT_TAG        main
)
FetchContent_Declare(
    NekoThreadPool
    GIT_REPOSITORY https://github.com/moehoshio/NekoThreadPool.git
    GIT_TAG        main
)
FetchContent_Declare(
    NekoFunction
    GIT_REPOSITORY https://github.com/moehoshio/NekoFunction.git
    GIT_TAG        main
)
FetchContent_Declare(
    NekoSystem
    GIT_REPOSITORY https://github.com/moehoshio/NekoSystem.git
    GIT_TAG        main
)
FetchContent_Declare(
    NekoNet
    GIT_REPOSITORY https://github.com/moehoshio/NekoNet.git
    GIT_TAG        main
)

FetchContent_MakeAvailable(NekoSchema NekoEvent NekoThreadPool NekoFunction NekoSystem NekoNet)

# ================
# ====  Info  ====
# ================

# default build type to Release
if(NOT CMAKE_BUILD_TYPE)
    set(CMAKE_BUILD_TYPE Release CACHE STRING "Choose the type of build.")
endif()

# Get the current git commit ID and build time
string(TIMESTAMP NEKO_BUILD_TIME "%Y%m%d%H%M%S" UTC)
execute_process(COMMAND git rev-parse --short HEAD OUTPUT_VARIABLE GIT_COMMIT_ID OUTPUT_STRIP_TRAILING_WHITESPACE)
add_definitions(
    -DBUILD_TIME="${NEKO_BUILD_TIME}"
    -DGIT_COMMIT_ID="${GIT_COMMIT_ID}"
    -DNEKO_LC_VERSION="${PROJECT_VERSION}"
    -DNEKO_LC_NAME="${PROJECT_NAME}"
    -DNEKO_LC_USER_VERSION="${USER_VERSION}"
    -DNEKO_LC_BUILD_TYPE="${CMAKE_BUILD_TYPE}"
    -DNEKO_LC_BUILD_COMPILER="${CMAKE_CXX_COMPILER_ID}"
    -DNEKO_LC_BUILD_COMPILER_VERSION="${CMAKE_CXX_COMPILER_VERSION}"
    -DNEKO_LC_BUILD_TIME="${NEKO_BUILD_TIME}"
)

# Print configuration summary
message(STATUS "NekoLauncher configuration summary:")
message(STATUS "  - CMake version: ${CMAKE_VERSION}")
message(STATUS "  - Build type: ${CMAKE_BUILD_TYPE}")
message(STATUS "  - Git commit ID: ${GIT_COMMIT_ID}")
message(STATUS "  - Project version: ${PROJECT_VERSION}")
message(STATUS "  - User version: ${NEKO_LC_USER_VERSION}")
message(STATUS "  - C++ Compiler: ${CMAKE_CXX_COMPILER_ID} ${CMAKE_CXX_COMPILER_VERSION}")
message(STATUS "  - Build time (UTC): ${NEKO_BUILD_TIME}")
message(STATUS "")
message(STATUS "Options:")
message(STATUS "  - Neko Auto fetch deps: ${NEKO_AUTO_FETCH_DEPS}")
message(STATUS "  - Neko Build tests: ${NEKO_BUILD_TESTS}")
message(STATUS "")
message(STATUS "Dependency summary:")
message(STATUS "  - Boost found: ${Boost_VERSION}")
message(STATUS "  - Qt6 found: ${Qt6_VERSION}")
message(STATUS "  - CMAKE_PREFIX_PATH: ${CMAKE_PREFIX_PATH}")
message(STATUS "  - Library dirs: ${NEKO_LIBRARY_DIRS}")
message(STATUS "  - Qt6 dir: ${Qt6_DIR}")
message(STATUS "  - STL dir: ${STL_DIR}")
message(STATUS "Neko Modules: NekoEvent, NekoNet, NekoSchema, NekoSystem, NekoFunction, NekoThreadPool")
message(STATUS "")


# ================
# === Add Src ====
# ================

# Include directories
list(APPEND NEKO_INCLUDE_DIRS
    ${CMAKE_SOURCE_DIR}/include
    ${NEKO_LIBRARY_DIRS}/include
    ${Qt6_DIR}/include
    ${STL_DIR}/include
)

# Src files
list(APPEND NEKO_SRCFILES
    # To ensure that qt moc can locate this file
    ${CMAKE_SOURCE_DIR}/include/neko/ui/windows/logViewerWindow.hpp
    ${CMAKE_SOURCE_DIR}/include/neko/ui/windows/nekoWindow.hpp

    # Ui
    ${CMAKE_SOURCE_DIR}/src/neko/ui/windows/nekoWindow.cpp
    ${CMAKE_SOURCE_DIR}/src/neko/ui/windows/logViewerWindow.cpp
    ${CMAKE_SOURCE_DIR}/src/neko/ui/widgets/headbarWidget.cpp
    ${CMAKE_SOURCE_DIR}/src/neko/ui/widgets/pixmapWidget.cpp
    ${CMAKE_SOURCE_DIR}/src/neko/ui/pages/homePage.cpp
    ${CMAKE_SOURCE_DIR}/src/neko/ui/pages/loadingPage.cpp
    ${CMAKE_SOURCE_DIR}/src/neko/ui/pages/settingPages.cpp
    ${CMAKE_SOURCE_DIR}/src/neko/ui/dialogs/hintDialog.cpp
    ${CMAKE_SOURCE_DIR}/src/neko/ui/dialogs/inputDialog.cpp

    # Core
    ${CMAKE_SOURCE_DIR}/src/neko/core/launcherProcess.cpp
    ${CMAKE_SOURCE_DIR}/src/neko/main.cpp
)

# ================
# == Executable ==
# ================

# Add the executable target
add_executable(NekoLc ${NEKO_SRCFILES})

# Set the target properties
target_include_directories(NekoLc PRIVATE ${NEKO_INCLUDE_DIRS})
target_compile_features(NekoLc PRIVATE cxx_std_20)

# Link Neko Modules
target_link_libraries(NekoLc PRIVATE 
    NekoEvent NekoNet NekoSchema NekoSystem NekoFunction NekoThreadPool
)
# Link libraries
target_link_libraries(NekoLc PRIVATE 
    # Qt
    Qt6::Core Qt6::Widgets Qt6::Gui
    # Boost
    Boost::system Boost::process
    # Others
    nlohmann_json
)


# Os-specific settings
if(WIN32)
    # Set not to use the console window
    if(CMAKE_BUILD_TYPE STREQUAL "Release")
        set_target_properties(NekoLc PROPERTIES WIN32_EXECUTABLE TRUE)
    endif()
    target_sources(NekoLc PRIVATE resource/windows/icon.rc)

elseif(APPLE)


    # Set the application bundle properties for macOS
    if(CMAKE_BUILD_TYPE STREQUAL "Release")
        set_target_properties(NekoLc PROPERTIES
            MACOSX_BUNDLE TRUE
            MACOSX_BUNDLE_BUNDLE_NAME "com.moehoshio.NekoLauncher"
            # MACOSX_BUNDLE_ICON_FILE "NekoLc.icns"
            MACOSX_BUNDLE_INFO_PLIST ${CMAKE_SOURCE_DIR}/resource/macos/Info.plist
        )
    endif()
    
    set(MACOSX_ICON ${CMAKE_SOURCE_DIR}/resource/macos/NekoLc.icns)
    target_sources(NekoLc PRIVATE ${MACOSX_ICON})
    set_source_files_properties(${MACOSX_ICON} PROPERTIES MACOSX_PACKAGE_LOCATION "Resources")

elseif(UNIX)

    # Set the executable properties for Linux
    set_target_properties(NekoLc PROPERTIES
        OUTPUT_NAME "NekoLc"
        PREFIX ""
        SUFFIX ""
    )

    # Add a custom command to copy the icon file to the resource directory
    add_custom_command(TARGET NekoLc POST_BUILD
        COMMAND ${CMAKE_COMMAND} -E copy ${CMAKE_SOURCE_DIR}/resource/linux/icon.png ${CMAKE_BINARY_DIR}/icon.png
        COMMENT "Copying icon.png to build directory"
    )

endif() # os


# ================
# ====  Test  ====
# ================

if(NEKO_BUILD_TESTS)
    enable_testing()
    message(STATUS "NekoLc tests enabled")

    if (NOT GTest_FOUND AND NOT NEKO_AUTO_FETCH_DEPS)
        message(FATAL_ERROR "GTest not found. Please install GoogleTest or enable NEKO_AUTO_FETCH_DEPS to automatically fetch it.")
    endif()

    include(GoogleTest)

    # Api struct to json
    add_executable(Api_toJson_test "${CMAKE_SOURCE_DIR}/test/Api_toJson_test.cpp")
    target_include_directories(Api_toJson_test PRIVATE ${NEKO_INCLUDE_DIRS})
    target_link_libraries(Api_toJson_test PRIVATE gtest_main)
    target_compile_features(Api_toJson_test PRIVATE cxx_std_20)
    
    gtest_discover_tests(Api_toJson_test)
else
    message(STATUS "NekoNet tests disabled")
endif()
        

# ================
# === Install ====
# ================

# Install rules
install(TARGETS NekoLc
    RUNTIME DESTINATION .
    BUNDLE DESTINATION .
)

# Install configuration and resource files
install(FILES ${CMAKE_SOURCE_DIR}/src/config.ini
    DESTINATION .
)

# Install language files
install(FILES 
    ${CMAKE_SOURCE_DIR}/src/lang/en.json
    ${CMAKE_SOURCE_DIR}/src/lang/zh_tw.json
    ${CMAKE_SOURCE_DIR}/src/lang/zh_cn.json
    DESTINATION lang
)

# Install image files
install(FILES ${CMAKE_SOURCE_DIR}/resource/img/loading.gif
    DESTINATION img
)